pomdp

observables
start
endobservables

const int axMAX = 3;
const int ayMAX = 2;
const int axMIN = 0;
const int ayMIN = 0;
const double slippery = 0.2;
const int ob1x = 1;
const int ob1y = 2;
const int ob2x = 3;
const int ob2y = 1;

formula done = start & ((ax = axMAX & ay = 0) | (ax = axMAX & ay = 2));
observable "amdone" = done;
formula crash =  (ax = ob1x & ay = ob1y) | (ax = ob2x & ay = ob2y) ;
observable "hascrash" = crash;


module master
    start : bool init false;

    [placement] !start -> (start'=true);
    [north] start & !done -> true;
    [south] start  & !done -> true;
    [east] start  & !done-> true;
    [west] start & !done -> true;


endmodule


module robot
    ax : [axMIN..axMAX] init 0;
    ay : [ayMIN..ayMAX] init 0;


    [placement] true -> (ax'=0) & (ay'=0); 

    [west] true -> (1-slippery): (ax'=max(ax-1,axMIN)) + slippery: (ax'=max(ax,axMIN));
    [east] true -> (1-slippery): (ax'=min(ax+1,axMAX)) + slippery: (ax'=min(ax,axMAX));
    [south]  true -> (1-slippery): (ay'=min(ay+1,ayMAX)) + slippery: (ay'=min(ay,ayMAX));
    [north]  true -> (1-slippery): (ay'=max(ay-1,ayMIN)) + slippery: (ay'=max(ay,ayMIN));
endmodule


label "goal" = done;
label "traps" = crash;
label "notbad" =  !crash;
