pomdp

observables
start
endobservables

const int N;

// 1/3 define  grid
const int axMAX = 5;
const int ayMAX = 5;
const int axMIN = 1;
const int ayMIN = 1;

const double slippery = 0.1;
const int ob1x = 4;
const int ob1y = 4;
const int ob2x = 5;
const int ob2y = 1;
const int ob3x = 1;
const int ob3y = 0;
const int ob4x = 5;
const int ob4y = 4;
const int ob5x = 2;
const int ob5y = 4;

//2/3 define goal
formula done = start & ((ax=5 & ay=5));

observable "amdone" = done;
formula crash =  (ax = ob1x & ay = ob1y) | (ax = ob2x & ay = ob2y)  | (ax = ob3x & ay = ob3y) | (ax = ob4x & ay = ob4y) | (ax = ob5x & ay = ob5y) ;
observable "hascrash" = crash;


module master
    start : bool init false;

    [placement] !start -> (start'=true);
    [north] start & !done -> true;
    [south] start  & !done -> true;
    [east] start  & !done-> true;
    [west] start & !done -> true;


endmodule


module robot
    ax : [axMIN..axMAX] init 3;
    ay : [ayMIN..ayMAX] init 3;

    //3/3 define initial
    [placement] true ->  1: (ax'=3) & (ay'=5) ;

    [west] ax >= 3 & ay >=3 -> (1-slippery): (ax'=max(ax-1,axMIN)) + slippery: (ax'=max(ax-2,axMIN));
    [east] ax >= 3 & ay >=3 -> (1-slippery): (ax'=min(ax+1,axMAX)) + slippery: (ax'=min(ax+2,axMAX));
    [south]  ax >= 3 & ay >=3 -> (1-slippery): (ay'=min(ay+1,ayMAX)) + slippery: (ay'=min(ay+2,ayMAX));
    [north]  ax >= 3 & ay >=3 -> (1-slippery): (ay'=max(ay-1,ayMIN)) + slippery: (ay'=max(ay-2,ayMIN));

    [west] ax < 3 | ay < 3 -> 1: (ax'=ax) & (ay'=ay);
    [east] ax < 3 | ay < 3  -> 1: (ax'=ax) & (ay'=ay);
    [south]  ax < 3 | ay < 3  -> 1: (ax'=ax) & (ay'=ay);
    [north]  ax < 3 | ay < 3  -> 1: (ax'=ax) & (ay'=ay);
endmodule


label "goal" = done;
label "traps" = crash;
label "notbad" =  !crash;

rewards "cost"

    //[west] ax = axMIN : 100;
    //[north] ay = ayMIN  : 100;
    //[south] ay = ayMAX : 100;

    [north] true : 1;
    [south] true : 1;
    [west] true : 1;
    [east] true : 1;
    
    crash: 5;
endrewards